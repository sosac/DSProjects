-- connie sosa : 4/12/2016 : ds730 : activity 7, task 11
-- activity7t11.hive
-- hive script to solve baseball problems
-- input files: Batting.csv, Master.csv

DROP TABLE IF EXISTS master;
CREATE EXTERNAL TABLE IF NOT EXISTS master(id STRING, byear INT, bmonth INT, bday INT, bcountry STRING, bstate STRING, bcity STRING, dyear INT, dmonth INT, dday INT, dcountry STRING, dstate STRING, dcity STRING, fname STRING, lname STRING, name STRING, weight INT, height INT, bats STRING, throws STRING, debut STRING, finalgame STRING, retro STRING, bbref STRING) ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' LOCATION '/home/hduser/hive/master';

DROP TABLE IF EXISTS batting;
CREATE EXTERNAL TABLE IF NOT EXISTS batting(id STRING, year INT, team STRING, league STRING, games INT, ab INT, runs INT, hits INT, doubles INT, triples INT, homeruns INT, rbi INT, sb INT, cs INT, walks INT, strikeouts INT, ibb INT, hbp INT, sh INT, sf INT, gidp INT) ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' LOCATION '/home/hduser/hive/batting';

-----------------------------------------------------------------
-- problem 1: heaviest player to hit > 5 triples in 2005

INSERT OVERWRITE DIRECTORY '/home/hduser/hive/answer1' 
SELECT m.lname, m.fname, m.id, m.weight 
FROM master m JOIN batting b ON (m.id = b.id) 
WHERE (b.year = 2005) AND (b.triples > 5) AND m.weight in 
(SELECT max(m2.weight)
   FROM master m2 JOIN batting b2 ON (m2.id = b2.id) 
   WHERE (b2.year = 2005) AND (b2.triples > 5));

-----------------------------------------------------------------
-- problem 2: player played for the most team in any single year

CREATE VIEW IF NOT EXISTS played_max_teams AS
SELECT b1.*
FROM (SELECT batting.id, batting.year, count(batting.team) AS total_teams
     FROM batting
     GROUP BY batting.id, batting.year) b1
JOIN (
SELECT MAX(b2.total_teams) AS max_teams 
FROM (SELECT b.id, b.year, count(b.team) AS total_teams
      FROM batting b
      GROUP BY b.id, b.year) b2
     ) b3
WHERE b1.total_teams = b3.max_teams;

INSERT OVERWRITE DIRECTORY '/home/hduser/hive/answer2' 
SELECT fname, lname, played_max_teams.*
FROM master JOIN played_max_teams
WHERE master.id = played_max_teams.id;

-----------------------------------------------------------------
-- problem 3: player with the most cumulative extra base hits during the 80s

CREATE VIEW IF NOT EXISTS most_exhits AS
SELECT q1.*
FROM (SELECT batting.id, SUM(doubles+triples+homeruns) AS extra_base_hit
     FROM batting
     WHERE year <= 1989 AND year >= 1980 AND doubles is not NULL AND triples is not NULL AND homeruns is not NULL
     GROUP BY id) q1
JOIN (
SELECT MAX(q2.extra_base_hit) AS max_eb_hit
FROM (SELECT b.id, SUM(b.doubles+b.triples+b.homeruns) AS extra_base_hit
     FROM batting b
     WHERE b.year <= 1989 AND b.year >= 1980 AND b.doubles is not NULL AND b.triples is not NULL AND b.homeruns is not NULL
     GROUP BY b.id) q2
     ) q3
WHERE q1.extra_base_hit = q3.max_eb_hit;

INSERT OVERWRITE DIRECTORY '/home/hduser/hive/answer3' 
SELECT fname, lname, most_exhits.*
FROM master JOIN most_exhits
WHERE master.id = most_exhits.id;

-----------------------------------------------------------------
-- problem 4: right handed batter with most hits in his career, who were born 
-- in oct, died in 2011

-- get right handed players born oct, died 2011
CREATE VIEW IF NOT EXISTS most_hits_R AS
SELECT b1.*
FROM (SELECT batting.id, SUM(batting.hits) AS most_hits
     FROM batting 
     WHERE batting.id in 
     (SELECT id FROM master WHERE bats = 'R' AND bmonth = 10 AND dyear = 2011)
     GROUP BY batting.id ) b1
JOIN (
SELECT MAX(b2.most_hits) AS most_hit
FROM (SELECT b.id, SUM(b.hits) AS most_hits
     FROM batting b
     WHERE b.id in 
     (SELECT id FROM master WHERE bats = 'R' AND bmonth = 10 AND dyear = 2011)
     GROUP BY b.id ) b2
     ) b3
WHERE b1.most_hits = b3.most_hit;

INSERT OVERWRITE DIRECTORY '/home/hduser/hive/answer4' 
SELECT fname, lname, most_hits_R.*
FROM master JOIN most_hits_R
WHERE master.id = most_hits_R.id;


